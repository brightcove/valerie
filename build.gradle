plugins {
    id 'java'
    id 'groovy'
    id 'maven-publish'
    id 'codenarc'
    id 'net.researchgate.release' version '2.2.2'
    id "com.jfrog.bintray" version "1.4"
}

repositories {
    jcenter()
}

configurations {
    checkerFrameworkAnnotatedJDK {
       description = 'a copy of JDK classes with Checker Framework type qualifers inserted'
    }
    checkerFramework {
       description = 'The Checker Framework: custom pluggable types for Java'
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.5'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'

    def checkerFrameworkVersion = '2.1.10'
    checkerFrameworkAnnotatedJDK "org.checkerframework:jdk8:${checkerFrameworkVersion}"
    checkerFramework "org.checkerframework:checker:${checkerFrameworkVersion}"
    compile "org.checkerframework:checker-qual:${checkerFrameworkVersion}"
}

allprojects {
    tasks.withType(JavaCompile).all { JavaCompile compile ->
        compile.options.compilerArgs = [
                '-processor', 'org.checkerframework.checker.nullness.NullnessChecker',
                '-processorpath', "${configurations.checkerFramework.asPath}",
                // uncomment to turn Checker Framework errors into warnings
                // '-Awarns',
                "-Xbootclasspath/p:${configurations.checkerFrameworkAnnotatedJDK.asPath}"
        ]
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        MainPublication(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.name
            version project.version
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['MainPublication']
    publish = true
    pkg {
        repo = 'valerie'
        name = 'valerie'
        userOrg = 'brightcove'
        desc = project.description
        websiteUrl = 'https://github.com/brightcove/valerie'
        issueTrackerUrl = 'https://github.com/brightcove/valerie/issues'
        vcsUrl = 'https://github.com/brightcove/valerie.git'
        licenses = ['Apache-2.0']
        version {
            name = project.version
            released = new Date()
        }
    }
}

bintrayUpload.onlyIf{
    !(version ==~ /.*SNAPSHOT/)
}

codenarc.config = resources.text.fromFile('config/codenarc/codenarc.groovy')

wrapper.gradleVersion = '3.3'
